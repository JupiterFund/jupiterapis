version = '0.0.1'

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

compileJava.options.encoding = 'UTF-8' 
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

repositories {
  mavenCentral()
}

buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
  }
}

dependencies {
  compile 'com.google.protobuf:protobuf-java:3.9.1'
  compile 'io.grpc:grpc-stub:1.23.0'
  compile 'io.grpc:grpc-protobuf:1.23.0'
  if (JavaVersion.current().isJava9Compatible()) {
    // Workaround for @javax.annotation.Generated
    // see: https://github.com/grpc/grpc-java/issues/3633
    compile 'javax.annotation:javax.annotation-api:1.3.1'
  }
  testCompile 'junit:junit:4.12'
}

sourceSets {
  main {
    proto {
      srcDir '.'
      include '**/*.proto'
      exclude 'build/**'
    }
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.9.1'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.23.0'
    }
  }
  generateProtoTasks {
    ofSourceSet('main')*.plugins {
      grpc { }
    }
  }
}

jar {
  manifest {
    attributes('Implementation-Title': project.name,
               'Implementation-Version': project.version)
  }
}
